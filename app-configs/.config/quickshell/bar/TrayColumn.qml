import QtQuick
import Quickshell
import Quickshell.Widgets
import Quickshell.Services.SystemTray
import "../common"

// StatusNotifierItem tray. Left-click activates, right-click opens the
// item's menu. Note: legacy XEmbed-only tray apps will not appear.
Column {
    id: root

    property var barWindow

    spacing: 6

    Repeater {
        model: SystemTray.items

        delegate: Item {
            id: trayItem

            required property var modelData

            width: 18
            height: 18

            IconImage {
                anchors.fill: parent
                source: trayItem.modelData.icon
            }

            QsMenuAnchor {
                id: menuAnchor
                menu: trayItem.modelData.menu
                anchor.window: root.barWindow
            }

            function openMenu() {
                const p = trayItem.mapToItem(null, trayItem.width, trayItem.height / 2);
                menuAnchor.anchor.rect = Qt.rect(p.x, p.y, 1, 1);
                menuAnchor.open();
            }

            MouseArea {
                anchors.fill: parent
                acceptedButtons: Qt.LeftButton | Qt.MiddleButton | Qt.RightButton
                onClicked: mouseEvent => {
                    if (mouseEvent.button === Qt.RightButton) {
                        if (trayItem.modelData.hasMenu)
                            trayItem.openMenu();
                    } else if (mouseEvent.button === Qt.MiddleButton) {
                        trayItem.modelData.secondaryActivate();
                    } else if (trayItem.modelData.onlyMenu) {
                        trayItem.openMenu();
                    } else {
                        trayItem.modelData.activate();
                    }
                }
            }
        }
    }
}
